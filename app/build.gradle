apply plugin: 'com.android.application'

android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        versionCode VERSION_CODE as int
        versionName VERSION_NAME
    }

    signingConfigs {
        release {
            storeFile file(keyStoreProperty)
            keyAlias keyAliasProperty
            storePassword keyStorePasswordProperty
            keyPassword keyAliasPasswordProperty
        }
    }

    buildTypes {
        debug {
            debuggable true
            zipAlignEnabled true
        }
        release {
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
            signingConfig signingConfigs.release
        }
    }
}

def getKeyStoreProperty() {
    return hasProperty('KEYSTORE') ? KEYSTORE : "debug.keystore"
}

def getKeyAliasProperty() {
    return hasProperty('KEYALIAS') ? KEYALIAS : "alias"
}

def getKeyStorePasswordProperty() {
    return hasProperty('KEYSTOREPASSWORD') ? KEYSTOREPASSWORD : "storepwd"
}

def getKeyAliasPasswordProperty() {
    return hasProperty('KEYALIASPASSWORD') ? KEYALIASPASSWORD : "aliaspwd"
}

dependencies {
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    compile 'org.jsoup:jsoup:1.10.2'
    compile 'com.zhy:base-rvadapter:3.0.3'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile "com.android.support:cardview-v7:${SUPPORT_PACKAGE_VERSION}"
    compile "com.android.support:design:${SUPPORT_PACKAGE_VERSION}"
    compile "com.android.support:appcompat-v7:${SUPPORT_PACKAGE_VERSION}"
    compile 'com.zhy:okhttputils:2.6.2'
    compile 'com.github.franmontiel:PersistentCookieJar:v1.0.1'
    compile 'jp.wasabeef:richeditor-android:1.2.2'
//    compile 'com.tencent.bugly:crashreport:latest.release'
}
