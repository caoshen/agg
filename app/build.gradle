apply plugin: 'com.android.application'
apply plugin: 'channel'

def loadSigningConfigs() {
    def Properties props = new Properties()
    def propFile = file('../signing.properties')
    if (propFile.canRead()) {
        props.load(new FileInputStream(propFile))
        if (props != null && props.containsKey('RELEASE_STORE_FILE') && props.containsKey('RELEASE_STORE_PASSWORD') &&
                props.containsKey('RELEASE_KEY_ALIAS') && props.containsKey('RELEASE_KEY_PASSWORD')) {
            android.signingConfigs.release.storeFile = file(props['RELEASE_STORE_FILE'])
            android.signingConfigs.release.storePassword = props['RELEASE_STORE_PASSWORD']
            android.signingConfigs.release.keyAlias = props['RELEASE_KEY_ALIAS']
            android.signingConfigs.release.keyPassword = props['RELEASE_KEY_PASSWORD']
        } else {
            android.buildTypes.release.signingConfig = null
        }
    } else {
        android.signingConfigs.release.storeFile = file(rootProject.getRootDir().getAbsolutePath() + "/release.jks")
        android.signingConfigs.release.storePassword = System.getenv("STORE_PASSWORD")
        android.signingConfigs.release.keyAlias = System.getenv("KEY_ALIAS")
        android.signingConfigs.release.keyPassword = System.getenv("KEY_PASSWORD")
    }
}

android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION


    defaultConfig {
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        versionCode VERSION_CODE as int
        versionName VERSION_NAME
        applicationId "cn.okclouder.ovc"
    }

    signingConfigs {
        release {
            v1SigningEnabled true
            v2SigningEnabled true
        }

        debug {

        }
    }

    loadSigningConfigs()

    buildTypes {
        debug {
            debuggable true
            zipAlignEnabled true
        }
        release {
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation "com.android.support:cardview-v7:${SUPPORT_PACKAGE_VERSION}"
    implementation "com.android.support:design:${SUPPORT_PACKAGE_VERSION}"
    implementation "com.android.support:appcompat-v7:${SUPPORT_PACKAGE_VERSION}"
    implementation "com.android.support:support-v4:${SUPPORT_PACKAGE_VERSION}"
    implementation 'org.jsoup:jsoup:1.10.2'
    implementation 'com.zhy:base-rvadapter:3.0.3'
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'de.hdodenhof:circleimageview:2.1.0'
    implementation 'com.zhy:okhttputils:2.6.2'
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'
    implementation 'com.tencent.bugly:crashreport_upgrade:1.3.4'
    implementation 'com.google.android:flexbox:0.3.0'
    implementation 'com.github.Aspsine:SwipeToLoadLayout:1.0.4'
    implementation 'com.github.Aspsine:IRecyclerView:0.0.5'
    implementation 'pub.devrel:easypermissions:1.0.1'
    implementation 'com.meituan.android.walle:library:1.1.5'
    implementation 'com.qmuiteam:qmui:1.0.6'
    implementation 'com.zhy:okhttputils:2.6.2'
    implementation 'com.alibaba:fastjson:1.2.38'
    testImplementation 'junit:junit:4.12'
    implementation project(':arch')
    implementation project(':library')
    implementation project(':account')
    api 'com.leon.channel:helper:1.1.7'

}

channel{
    //指定渠道文件
    channelFile = file(rootProject.getRootDir().getAbsolutePath() + "/channel.txt")
    //多渠道包的输出目录，默认为new File(project.buildDir,"channel")
    baseOutputDir = new File(project.buildDir,"channel")
    //多渠道包的命名规则，默认为：${appName}-${versionName}-${versionCode}-${flavorName}-${buildType}
    apkNameFormat ='${appName}-${versionName}-${versionCode}-${flavorName}-${buildType}'
    //快速模式：生成渠道包时不进行校验（速度可以提升10倍以上）
    isFastMode = false
}